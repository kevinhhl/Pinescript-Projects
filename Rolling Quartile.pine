// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© DojiEmoji

//@version=5
indicator("Quartiles with Box Plot [KL]", overlay=true)

// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
// Settings
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
float src            = input.source(close, title="Source data")
var int n            = input.int(20, title="Lookback", minval=1)
var int offset       = input.int(5, minval=5, maxval=150, title="Offset: Box Plot", step=5, tooltip="Relative to recenrt bar (right hand side)")

var string GROUP_1   = "Box plot"
var color color1     = input.color(color.blue, title="Box plot", group=GROUP_1)
var int ln_width     = input.int(2, title="Width", minval=1, maxval=4, group=GROUP_1)

var bool show_hline_iqr    = input.bool(true, title="Show lines for rolling IQR", group=GROUP_1)
var bool show_hline_maxmin = input.bool(false, title="Show lines for rolling Min & Max", group=GROUP_1)

var string group_2       = "Historical IQR - Moving lines"
var string _tt           = "Go to 'Style' tab to modify color/width"
var bool show_bands_iqr  = input.bool(false, title="Show moving IQR", group=group_2, tooltip=_tt)
var bool show_bands_mm   = input.bool(false, title="Show moving Min & Max", group=group_2, tooltip=_tt)

// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
// Quartiles
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

float Q0 = ta.percentile_linear_interpolation(src, n, 0)
float Q1 = ta.percentile_linear_interpolation(src, n, 25)
float Q2 = ta.percentile_linear_interpolation(src, n, 50)
float Q3 = ta.percentile_linear_interpolation(src, n, 75)
float Q4 = ta.percentile_linear_interpolation(src, n, 100)

// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
// Drawing Box Plot - to represent the quartiles
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

// Objects used:        Representing:
var line ln1 = na       // Median
var line ln2a = na      // Vertical lines for Whiskers
var line ln2b = na      // ''
var line ln3a = na      // ''
var line ln3b = na      // ''
var line ln4q0 = na     // Horizontal lines, marking Q0 to Q4
var line ln4q1 = na     // ''
var line ln4q2 = na     // ''
var line ln4q3 = na     // ''
var line ln4q4 = na     // ''
var box bx1 = na        // IQR

// Helper functions {

var line[] dump_lns = array.new_line()
draw_line(offset_x1, y1, offset_x2, y2, ln_color, ln_width, ln_style=line.style_solid) =>
    ln = line.new(bar_index+offset_x1, y1, bar_index+offset_x2, y2, xloc.bar_index, color=ln_color, width=ln_width, style=ln_style)
    array.push(dump_lns, ln)
    ln

del_lines() =>
    while array.size(dump_lns) > 0
        line.delete(array.pop(dump_lns)) 

draw_IQR() =>
    box.new(bar_index+offset-2, Q3, bar_index+offset+2, Q1, border_color=color1, bgcolor=na, border_width=ln_width)
// }

// Clean up, prior iteration
del_lines()
box.delete(bx1[1])

// Box and whisker
ln1 := draw_line(offset-2, Q2, offset+2, Q2, color1, ln_width)
ln2a := draw_line(offset, Q1, offset, Q0, color1, ln_width)
ln2b := draw_line(offset-1, Q0, offset+1, Q0, color1, ln_width)
ln3a := draw_line(offset, Q3, offset, Q4, color1, ln_width)
ln3b := draw_line(offset-1, Q4, offset+1, Q4, color1, ln_width)
bx1 := draw_IQR()

// Rolling quartiles
if show_hline_iqr
    ln4q1 := draw_line(-n, Q1, offset-4, Q1, color1, 1, line.style_dotted)
    ln4q2 := draw_line(-n, Q2, offset-4, Q2, color1, 1, line.style_dotted)
    ln4q3 := draw_line(-n, Q3, offset-4, Q3, color1, 1, line.style_dotted)
if show_hline_maxmin
    ln4q0 := draw_line(-n, Q0, offset-4, Q0, color1, 1, line.style_dotted)
    ln4q4 := draw_line(-n, Q4, offset-4, Q4, color1, 1, line.style_dotted)

// Moving quartiles
plot(Q1, color=show_bands_iqr ? color1 : na, title="Q1 Lower band")
plot(Q2, color=show_bands_iqr ? color1 : na, title="Q2 Center line")
plot(Q3, color=show_bands_iqr ? color1 : na, title="Q3 Upper band")

plot(Q0, color=show_bands_mm ? color1 : na, title="Q0 Min band")
plot(Q4, color=show_bands_mm ? color1 : na, title="Q4 Max band")
