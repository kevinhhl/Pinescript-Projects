// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© DojiEmoji

//@version=5
indicator("BB w/ COV [DojiEmoji]", overlay=true)

//------------------------------
// Source data && Stats.
//------------------------------
var string GROUP_BASIS = "Source"
var int length         = input.int(20, minval=1, group=GROUP_BASIS, title="Lookback period")
float src              = input(close, title="Source", group=GROUP_BASIS)

float mean  = ta.sma(src, length)
float stdev = ta.stdev(src, length)

// Coefficient of variation
float CoV   = stdev / mean

//------------------------------
// Visualizations
//------------------------------
var string GROUP_VIZ = "Visualizations: Coefficient of Variation (COV)"
var color col_min    =input.color(color.new(color.black,50), title="Low CoV", group=GROUP_VIZ, inline="col")
var color col_max    =input.color(color.new(color.yellow,50), title="High CoV", group=GROUP_VIZ, inline="col",
 tooltip="Will compare COV at each bar over the past n bars, and then display ranking in terms of percentiles using a color scale.")

plot(mean, title="Center line")

fill(
 plot(2 * stdev + mean, color=na),
 plot(-2 * stdev + mean, color=na),
 color=color.from_gradient(CoV, ta.lowest(CoV,length), ta.highest(CoV,length), col_min, col_max))

//------------------------------
// Signals
//------------------------------
bool is_ranging = CoV < ta.median(CoV, length)
bgcolor(is_ranging ? col_min : na, title="Ranging")
